{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 命令型モデル\n",
    "\n",
    "- 命令型モデルでは、「計算」は「命令を実行する」ことと捉える\n",
    "    - 命令: 計算機の状態を変更する処理\n",
    "    - 命令を実行することで計算機の記憶装置や表示装置の中身が変化していき、最終的に得られた状態を計算の結果と考える\n",
    "\n",
    "## チューリングマシン\n",
    "\n",
    "- チューリングマシン: 命令型モデルの原型\n",
    "    - 左右に無限の長さを持つ「テープ」に接する「ヘッド」を持つ\n",
    "    - 「ヘッド」は「テーブル」と呼ばれる制御装置により左右に移動され、テープから情報を読み書きする\n",
    "- チューリングマシンの処理:\n",
    "    1. ヘッドが接するテープのマス目から情報を読み込む\n",
    "    2. ヘッドが接するテープのマス目に情報を書き込む\n",
    "    3. ヘッドをテープ上で左右に1つ移動する\n",
    "    4. テーブルに格納されている状態を変更する\n",
    "    5. テーブルに格納されている状態が最終状態に到達するまで 1 - 4 の処理を反復する\n",
    "\n",
    "### 定義\n",
    "- `TuringTape::Dict{Int, TuringValue}`: テープ\n",
    "    - `TuringValue::Union{Val{0}, Val{1}, Nothing}`: テープに書き込み可能な値\n",
    "        - `val(::Val{T}) where {T} = T`: TuringValue の値を取得する関数\n",
    "    - 左右に無限の長さを持つ配列\n",
    "    - テープの各マスには TuringValue (value: 0 or 1 or nothing) を書き込み可能\n",
    "- `TuringCommand::struct`: コマンド構造体\n",
    "    - Fields:\n",
    "        - `write::TuringValue`: テープに書き込む値\n",
    "        - `move::Union{Val{-1}, Val{1}}`: ヘッドを動かす方向\n",
    "        - `next::Symbol`: 次の状態\n",
    "- `TuringInstruction::struct`: 命令構造体\n",
    "    - Fields:\n",
    "        - `command::TuringCommand`: ヘッドが接するテープのマス目に値が設定されていない場合に実行されるコマンド\n",
    "        - `command0::TuringCommand`: ヘッドが接するテープのマス目の値が 0 の場合に実行されるコマンド\n",
    "        - `command1::TuringCommand`: ヘッドが接するテープのマス目の値が 1 の場合に実行されるコマンド\n",
    "- `TuringProgram::Dict{Symbol, TuringInstruction}`: チューリングマシンに実行させるプログラム\n",
    "    - Fields:\n",
    "        - `@key::Symbol`: 状態\n",
    "        - `@val::TuringInstruction`: 状態に対応する命令構造体\n",
    "- `TuringHead::mutable struct`: ヘッド\n",
    "    - Fields:\n",
    "        - `position::Int`: ヘッド位置\n",
    "    - Functions:\n",
    "        - `read(self::TuringHead, tape::TuringTape) ::TuringValue`: ヘッドが接するテープのマス目から値を読み取る\n",
    "        - `write!(tape::TuringTape, self::TuringHead, value::TuringValue)`: ヘッドが接するテープのマス目に値 (0 or 1 or nothing) を書き込む\n",
    "        - `move!(self::TuringHead, direction::Union{Value{1}, Value{-1}})`: ヘッドを前か後ろに一つ移動する\n",
    "- `TuringMachine::mutable struct`: チューリングマシン\n",
    "    - Fields:\n",
    "        - `head::TuringHead`: ヘッド\n",
    "        - `tape::TuringTape`: テープ\n",
    "        - `state::Symbol`: 現在の状態\n",
    "        - `startState::Symbol`: 初期状態\n",
    "        - `endState::Symbol`: 終了状態\n",
    "        - `program::TuringProgram`: 実行するプログラム\n",
    "    - Functions:\n",
    "        - `isend(self::TuringMachine) ::Bool`: チューリングマシンが終了状態か判定\n",
    "        - `setstate!(self::TuringMachine, state::Symbol) ::MaybeTuringInstruction`: チューリングマシンの状態を変更し、次に実行する注文構造体を取得する\n",
    "            - `MaybeTuringInstruction::Union{TuringInstruction, Nothing}`: TuringInstruction or nothing\n",
    "        - `getcommand(self::TuringMachine, instruction::TuringInstruction) ::TuringCommand`: ヘッドが指すテープ情報から次に実行するコマンドを取得する\n",
    "        - `execute!(self::TuringMachine, command::TuingCommand) ::MaybeTuringInstructure`: コマンドを実行（テープへの書き込み・ヘッドの移動を実行）し、次の状態へ遷移する\n",
    "        - `run!(self::TuringMachine)`: プログラム実行\n",
    "        - `str(self::TuringMachine) ::AbstractString`: チューリングマシンテープの値を文字列として取得 (nothing は `.` に変換される)\n",
    "        - `val(self::TuringMachine) ::Int`: チューリングマシンテープの値を10進数の数値として取得"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Dict{Int64, Union{Val{1}, Val{0}, Nothing}} with 4 entries:\n",
       "  0 => nothing\n",
       "  2 => Val{1}()\n",
       "  3 => nothing\n",
       "  1 => Val{1}()"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "data": {
      "text/plain": [
       "\".11.\""
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "data": {
      "text/plain": [
       "3"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using Match\n",
    "\n",
    "###############\n",
    "# definitions #\n",
    "###############\n",
    "val(v::Val{T}) where T = T\n",
    "\n",
    "const TuringValue = Union{Val{0}, Val{1}, Nothing}\n",
    "const TuringTape = Dict{Int, TuringValue}\n",
    "const TuringHeadMoveDirection = Union{Val{-1}, Val{1}}\n",
    "\n",
    "struct TuringCommand\n",
    "    write::TuringValue\n",
    "    move::TuringHeadMoveDirection\n",
    "    next::Symbol\n",
    "end\n",
    "\n",
    "struct TuringInstruction\n",
    "    command::TuringCommand\n",
    "    command0::TuringCommand\n",
    "    command1::TuringCommand\n",
    "end\n",
    "\n",
    "const MaybeTuringInstruction = Union{TuringInstruction, Nothing}\n",
    "const TuringProgram = Dict{Symbol, TuringInstruction}\n",
    "\n",
    "mutable struct TuringHead\n",
    "    position::Int\n",
    "end\n",
    "\n",
    "read(self::TuringHead, tape::TuringTape) ::TuringValue = haskey(tape, self.position) ? tape[self.position] : nothing\n",
    "write!(tape::TuringTape, self::TuringHead, value::TuringValue) = (tape[self.position] = value)\n",
    "move!(self::TuringHead, direction::TuringHeadMoveDirection) = (self.position += val(direction))\n",
    "\n",
    "mutable struct TuringMachine\n",
    "    head::TuringHead\n",
    "    tape::TuringTape\n",
    "    state::Symbol\n",
    "    startState::Symbol\n",
    "    endState::Symbol\n",
    "    program::TuringProgram\n",
    "end\n",
    "\n",
    "TuringMachine(program::TuringProgram; tape::TuringTape = TuringTape(), startState::Symbol = :start, endState::Symbol = :end) = TuringMachine(\n",
    "    TuringHead(1),\n",
    "    tape,\n",
    "    startState,\n",
    "    startState,\n",
    "    endState,\n",
    "    program\n",
    ")\n",
    "\n",
    "isend(self::TuringMachine) ::Bool = self.state === self.endState\n",
    "\n",
    "setstate!(self::TuringMachine, state::Symbol) ::MaybeTuringInstruction = begin\n",
    "    self.state = state\n",
    "    haskey(self.program, self.state) ? self.program[self.state] : nothing\n",
    "end\n",
    "\n",
    "getcommand(self::TuringMachine, instruction::TuringInstruction) ::TuringCommand = @match read(self.head, self.tape) begin\n",
    "    Val(0) => instruction.command0\n",
    "    Val(1) => instruction.command1\n",
    "    _ => instruction.command\n",
    "end\n",
    "\n",
    "execute!(self::TuringMachine, command::TuringCommand) ::MaybeTuringInstruction = begin\n",
    "    write!(self.tape, self.head, command.write)\n",
    "    move!(self.head, command.move)\n",
    "    setstate!(self, command.next)\n",
    "end\n",
    "\n",
    "run!(self::TuringMachine) = begin\n",
    "    nextInstruction = setstate!(self, self.startState)\n",
    "\n",
    "    while nextInstruction !== nothing && !isend(self)\n",
    "        command = getcommand(self, nextInstruction)\n",
    "        nextInstruction = execute!(self, command)\n",
    "    end\n",
    "end\n",
    "\n",
    "str(self::TuringMachine) ::AbstractString = map(sort(collect(self.tape); by = pair -> pair[1])) do pair\n",
    "    isnothing(pair[2]) ? \".\" : string(val(pair[2]))\n",
    "end |> join\n",
    "\n",
    "val(self::TuringMachine) ::Int = filter(sort(collect(self.tape); by = pair -> pair[1])) do pair\n",
    "    !isnothing(pair[2])\n",
    "end |> tape -> map(tape) do pair\n",
    "    string(val(pair[2]))\n",
    "end |> join |> joinedTape -> parse(Int, joinedTape; base = 2)\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "@test テープに記述された数値に1を加算するチューリングマシン\n",
    "\"\"\"\n",
    "# 初期値 `2` を表すテープ: 2進数 `10`\n",
    "tape = TuringTape(\n",
    "    1 => Val(1),\n",
    "    2 => Val(0)\n",
    ")\n",
    "\n",
    "# チューリングマシンに与えるプログラム: テープに記述された値に1を加算\n",
    "program = TuringProgram(\n",
    "    :q0 => TuringInstruction(\n",
    "        TuringCommand(nothing, Val(-1), :q1),\n",
    "        TuringCommand(Val(0), Val(1), :q0),\n",
    "        TuringCommand(Val(1), Val(1), :q0)\n",
    "    ),\n",
    "    :q1 => TuringInstruction(\n",
    "        TuringCommand(Val(1), Val(-1), :q3),\n",
    "        TuringCommand(Val(1), Val(-1), :q2),\n",
    "        TuringCommand(Val(0), Val(-1), :q1)\n",
    "    ),\n",
    "    :q2 => TuringInstruction(\n",
    "        TuringCommand(nothing, Val(1), :q4),\n",
    "        TuringCommand(Val(0), Val(-1), :q2),\n",
    "        TuringCommand(Val(1), Val(-1), :q2)\n",
    "    ),\n",
    "    :q3 => TuringInstruction(\n",
    "        TuringCommand(nothing, Val(1), :q4),\n",
    "        TuringCommand(Val(0), Val(1), :q4),\n",
    "        TuringCommand(Val(1), Val(1), :q4)\n",
    "    )\n",
    ")\n",
    "\n",
    "machine = TuringMachine(program; tape = tape, startState = :q0, endState = :q4)\n",
    "run!(machine)\n",
    "\n",
    "display(machine.tape)\n",
    "display(str(machine))\n",
    "display(val(machine))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 命令型モデルの特徴\n",
    "\n",
    "- 計算機に命令を下し、計算機はその命令を実行するたびに内部の状態を変化させ、最終的に到達した状態が計算の結果となる\n",
    "    - チューリングマシンの場合:\n",
    "        - 命令:\n",
    "            - `program::TuringProgram`\n",
    "        - 状態:\n",
    "            - `TuringMachine.tape::TuringTape`\n",
    "            - `TuringMachine.state::Symbol`\n",
    "            - `TuringMachine.head.position::Int`\n",
    "- 「命令」は処理の一連の流れを記述したものであるため、ある観点から見るととても直感的\n",
    "    - 例えば、誰かに買い物を頼むとき、店までの道順と買う品物をすべて列挙して頼むことに似ている\n",
    "    - 買い物を頼まれた側は、依頼された行動を単純に上から順に実行していくだけで良い\n",
    "- 一方、命令の内容を見ても分かる通り、命令型モデルでは命令の定義が冗長でわかりにくい側面もある\n",
    "    - これは、チューリングマシンの「状態」が把握しづらく、かつその把握しづらい状態を常に意識して命令を作らなければならないことに起因する\n",
    "    - チューリングマシンの場合は「テープ」「状態」「ヘッド位置」という3つの状態があり、これらを命令の中で明示的に指し示す方法がないため、プログラムが必要以上に複雑化してしまう"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
